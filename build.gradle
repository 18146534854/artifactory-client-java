apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'artifactory'
apply plugin: 'maven'

buildscript {
    repositories {
        maven { url 'http://repo.jfrog.org/artifactory/gradle-plugins' }
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.0.12')
    }
}

artifactory {
    contextUrl = 'http://repo.jfrog.org/artifactory' //The base Artifactory URL if not overridden by the publisher/resolver
    resolve {
        repository {
            repoKey = 'libs-releases'
            maven = true
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.1'
}

group = 'com.jfrog.artifactory.client'
version = '1-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '2.0.1'
    groovy group: 'org.codehaus.groovy', name: 'groovy-json', version: '2.0.1'
    groovy group: 'org.codehaus.groovy', name: 'groovy-xml', version: '2.0.1'
    compile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.5.2'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.0.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.0.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.4'
    testCompile group: 'org.testng', name: 'testng', version: '6.5.2'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}
jar.dependsOn sourceJar

configurations {
    allArchives
}
artifacts {
    allArchives sourceJar, jar
}
install.configuration = configurations.allArchives

test {
    useTestNG()
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
        wildcards += '?*.gradle'
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                // Use git
                def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
                vcsConfig.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}